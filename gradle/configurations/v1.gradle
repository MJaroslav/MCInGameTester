repositories { maven { url 'https://jitpack.io' } }

task testJar(type: Jar, description: "Make temp jar from test source set",
        group: "MCInGameTester") {
    from sourceSets.test.output
    destinationDir getTemporaryDir()
}

task copyMappingsToProjectDir(type: Copy, dependsOn: 'genSrgs',
        description: "Just copies mappings to build dir, it needed if you use CodeChickenLib (from GregTech maven)",
        group: "MCInGameTester") {
    from plugins.getPlugin('forge').delayedFile('{MCP_DATA_DIR}')
    include '**/*.srg'
    include "**/*.csv"
    into new File(tasks.testJar.getTemporaryDir(), "../conf")
}

// Just wrapper for runClient task with using test source set
task testClient(type: JavaExec, dependsOn: ["makeStart", "copyMappingsToProjectDir", "jar", "testJar"],
        group: "MCInGameTester", description: "Runs the Minecraft Client, runs game tests then stop it") {
    doFirst {
        var f = getTemporaryDir(); f.deleteDir(); f.mkdirs() // Always clear server
    }
    // Outputs removed for avoiding mod duplications
    // Uses jars for manifests
    classpath project.configurations.testRuntimeClasspath + files(tasks.jar) + files(tasks.testJar) - sourceSets.main.output - sourceSets.test.output
    main tasks.runClient.main
    args tasks.runClient.args
    jvmArgs tasks.runClient.jvmArgs + (System.getenv("ONlY_TEST_LOG") != null ? ["-Dlog4j.configurationFile=mcingametester.xml"] : []) // WTF WHY DON'T WORK ON MY PC??
    workingDir getTemporaryDir()
    standardOutput tasks.runClient.standardOutput
    errorOutput tasks.runClient.errorOutput
}

// Just wrapper for runServer task with using test source set
task testServer(type: JavaExec, dependsOn: ["makeStart", "copyMappingsToProjectDir", "jar", "testJar"],
        group: "MCInGameTester", description: "Runs the Minecraft Server, runs game tests then stop it") {
    doFirst {
        var f = getTemporaryDir(); f.deleteDir(); f.mkdirs() // Always clear server
        new File(getTemporaryDir(), "eula.txt").setText("eula=true") // auto eula
    }
    // Outputs removed for avoiding mod duplications
    // Uses jars for manifests
    classpath project.configurations.testRuntimeClasspath + files(tasks.jar) + files(tasks.testJar) - sourceSets.main.output - sourceSets.test.output
    main tasks.runServer.main
    args tasks.runServer.args
    jvmArgs tasks.runServer.jvmArgs + (System.getenv("ONlY_TEST_LOG") != null ? ["-Dlog4j.configurationFile=mcingametester.xml"] : []) // WTF WHY DON'T WORK ON MY PC??
    println(jvmArgs)
    workingDir getTemporaryDir()
    standardOutput tasks.runServer.standardOutput
    errorOutput tasks.runServer.errorOutput
}

dependencies {
    testImplementation "com.github.MJaroslav:MCInGameTester:1.0.3:dev" // dev for deobf version
}
