import org.gradle.work.DisableCachingByDefault

@DisableCachingByDefault(because = 'Not worth caching')
abstract class TestGameTask extends JavaExec {
    @Input
    abstract Property<Boolean> getClearWorkingDirBeforeLaunch()

    @Input
    abstract Property<Boolean> getEula()

    @Input
    abstract Property<Boolean> getLogOnlyTests()

    @Input
    abstract Property<Boolean> getCopyMappingsLocally()

    TestGameTask() {
        super()
        dependsOn 'makeStart', 'genSrgs', 'jar', 'testJar'
        group 'MCInGameTester'
    }

    @TaskAction
    @Override
    void exec() {
        def file = getWorkingDir()
        if (getClearWorkingDirBeforeLaunch()) { file.deleteDir(); file.mkdirs() }
        if (getEula()) new File(file, "eula.txt").setText("eula=true")
        if (getLogOnlyTests()) systemProperty 'log4j.configurationFile', 'mcingametester.xml'
        if (getCopyMappingsLocally()) project.copy {
            from project.plugins.getPlugin('forge').delayedFile('{MCP_DATA_DIR}')
            include '**/*.srg'
            include "**/*.csv"
            into new File(getWorkingDir(), "../conf")
        }
        classpath project.configurations.testRuntimeClasspath + project.files(project.tasks.jar) + project.files(project.tasks.testJar) - project.sourceSets.main.output - project.sourceSets.test.output
        standardOutput System.out
        errorOutput System.err
        standardInput System.in
        super.exec()
    }
}

task testJar(type: Jar, description: 'Make temp jar from test source set',
        group: 'MCInGameTester') {
    from sourceSets.test.output
    destinationDir getTemporaryDir()
}

tasks.register('testClient', TestGameTask) {
    clearWorkingDirBeforeLaunch = true
    eula = false
    logOnlyTests = true
    copyMappingsLocally = true
    description 'Runs the Minecraft Client, runs game tests then stop it'
    setMain(tasks.runClient.main)
    setWorkingDir(getTemporaryDir())
}

tasks.register('testServer', TestGameTask) {
    clearWorkingDirBeforeLaunch = true
    eula = true
    logOnlyTests = true
    copyMappingsLocally = true
    description 'Runs the Minecraft Server, runs game tests then stop it'
    args args + "--nogui"
    setMain(tasks.runServer.main)
    setWorkingDir(getTemporaryDir())
}

repositories { maven { url 'https://jitpack.io' } }

dependencies {
    testImplementation 'com.github.MJaroslav:MCInGameTester:1.0.3:dev' // dev for deobf version
}
